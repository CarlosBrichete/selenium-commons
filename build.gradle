plugins {
    id "com.jfrog.bintray" version "1.7"
    id "de.undercouch.download" version "3.1.2"
}


repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'eclipse'


publishing {
	publications {
	    mavenJava(MavenPublication) {
    		from components.java
				artifact sourcesJar
            	artifact javadocJar
        }
	}
}

description = """Liferay GS Selenium Commons Framework"""

group = 'com.liferay.gs'

// Version X.Y.Z = X, When changing the API
// Version X.Y.Z = Y, When adding features while maintaining compatibility
// Version X.Y.Z = Z, When to fix failures while maintaining compatibility
version = '2.1.3'

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'lfrgs-selenium-commons'
        desc = 'lfrgs-selenium-commons'
		licenses = ['Apache-2.0']
		userOrg = System.getenv('BINTRAY_USER')
        labels = ['lfrgs-selenium-commons', 'test', 'selenium', 'liferay', 'gs']
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/manoelcyreno/lfrgs-selenium-commons.git'
        version {
            name = project.version
            desc = 'lfrgs-selenium-commons: Selenium framework for Liferay GS projects'
            released  = new Date()
            vcsTag = project.version
        }
    }
}

ext.junitVersion = '4.12'
ext.seleniumVersion = '3.4.0'

dependencies {
	compile group: 'junit', name: 'junit', version:junitVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-api', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-remote-driver', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-support', version:seleniumVersion
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

// A custom task to show report on tests that have run
task viewResults(dependsOn: ['test'] , type:Exec) {
		workingDir './build/reports/tests'
		commandLine 'cmd', '/c', 'start index.html'
}