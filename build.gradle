/*
	gradle clean test 		- Rebuild and run tests
	gradle test 			- Run tests only
	gradle cleanTest test  	- Force tests to be run even if up to date
	gradle viewResults		- Displays the report of test results (Windows only)
*/

// Support for building and testing

plugins {
    id "com.jfrog.bintray" version "1.7"
    id "de.undercouch.download" version "3.1.2"
}


repositories {
  jcenter()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'


publishing {
	publications {
		selelinumBintrayPub(MavenPublication) {
			from components.java
			groupId 'com.liferay.gs.commons'
      artifactId 'lfrgs-selenium-commons'
      version '0.4.5'
		}
	}
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['selelinumBintrayPub']
    pkg {
        repo = 'maven'
        name = 'lfrgs-selenium-commons'
        userOrg = System.getenv('BINTRAY_USER')
        labels = ['test', 'selenium', 'liferay', 'gs']
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/gallindo/lfrgs-selenium-commons.git'
        version {
            name = '0.4.5'
            desc = 'Selenium framework for Liferay GS projects'
            released  = new Date()
            vcsTag = '0.4.5'
            //attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

// Cf. Maven properties
ext.junitVersion = '4.12'
ext.seleniumVersion = '3.0.0'


// Cf. Maven <dependencies>
dependencies {
	compile group: 'junit', name: 'junit', version:junitVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-api', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-remote-driver', version:seleniumVersion
	compile group: 'org.seleniumhq.selenium', name: 'selenium-support', version:seleniumVersion
}

// A custom task to show report on tests that have run
task viewResults(dependsOn: ['test'] , type:Exec) {
		workingDir './build/reports/tests'
		commandLine 'cmd', '/c', 'start index.html'
}

task setupSelenium(){

    def configurationProperties = new Properties()
    file("src/main/resources/com/liferay/gs/testFramework/configuration.properties").withInputStream {
        stream -> configurationProperties.load(stream)
    }

    def defaultPropertiesPath = configurationProperties['defaultPropertiesFile']
    def phantomJSPath = configurationProperties['PhantomJS_Path']
    def geckoDriverPath = configurationProperties['GeckoDriver_Path']
    def webDriversPath =  configurationProperties['webDrivers_Path']
    def webDriversFile = new File( webDriversPath )

    /*
     * ********************************************************
     * CREATE PROPERTIES FILE
     * ********************************************************
     */
    def defaultPropertiesFile = new File( defaultPropertiesPath )

    if( !defaultPropertiesFile.exists() ) {
      defaultPropertiesFile.getParentFile().mkdirs()

      defaultPropertiesFile.withWriterAppend { w ->
          w <<  "environment=http://www.google.com.br\nlinkToLogOut=:8080/c/portal/logout\nSeleniumGridMachine=http://cloud-10-0-40-32:5555/wd/hub\n\n#The browser atribute to use SELENIUM GRID should be received: firefox, chrome, ie.\n#For use a localhost configuration (Selenium IDE) set to use \"default\" for use the Firefox or phantomjs.\nbrowser=default\ntime-out=30\n\n#The standard of the files should be: 01.jpg until 05.jpg\n#This Path will be used on Remote Browsers (by Selenium Grid)\nPathWithAttachFiles=C:\\\\screenshots\\\n"
      }
    }


    /*
     * ********************************************************
     * INSTALL PHANTOMJS
     * ********************************************************
     */
    def phantomJSFile = new File( phantomJSPath )

    if( !phantomJSFile.exists() ) {
        webDriversFile.mkdirs()

        download {
            src 'https://bitbucket.org/ariya/phantomjs/downloads/phantomjs-2.1.1-macosx.zip'
            dest new File(webDriversPath, 'phantomjs-2.1.1-macosx.zip')
        }

        copy {
            from zipTree(new File(webDriversPath, 'phantomjs-2.1.1-macosx.zip'))
            into webDriversFile
        }

        def phantomJSDir = new File(webDriversPath, 'phantomjs-2.1.1-macosx')
        phantomJSDir.renameTo(new File(webDriversPath, 'phantomjs'))
    }

    /*
     * ********************************************************
     * INSTALL GECKO DRIVER
     * ********************************************************
     */
    def geckoDriverFile = new File( geckoDriverPath )

    if( !geckoDriverFile.exists() ) {
        webDriversFile.mkdirs()

        download {
            src 'https://github.com/mozilla/geckodriver/releases/download/v0.11.1/geckodriver-v0.11.1-macos.tar.gz'
            dest new File(webDriversPath, 'geckodriver-v0.11.1-macos.tar.gz')
        }

        copy {
            from tarTree(new File(webDriversPath, 'geckodriver-v0.11.1-macos.tar.gz'))
            into webDriversFile
        }
    }
}

// Resolve Maven dependencies as Maven does
repositories {
	mavenCentral()
	mavenLocal()
}
